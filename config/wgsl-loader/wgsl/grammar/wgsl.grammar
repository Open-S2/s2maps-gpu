@top Program { unit }
@skip { space | comment }
@external tokens untilEOLToken from "./tokens" { untilEOL }
@external tokens untilCommentCloseToken from "./tokens" { untilCommentClose }

unit { globalDirective* globalDeclaration* }

b<term> { @specialize[@name='Boolean']<Identifier, term> }
k<term> { @specialize[@name='Keyword']<Identifier, term> }
t<term> { @specialize[@name='Type']<Identifier, term> }
d<term> { @specialize[@name='Directive']<Identifier, term> }
r<term> { @specialize[@name='Reserved']<Identifier, term> }

comment { commentLine untilEOL | commentOpen untilCommentClose }
template<content> { '<' content '>' }

globalDirective {
  EnableDirective
}

globalDeclaration {
  semicolon |
  LocalDeclaration |
  ImportDeclaration
}

LocalDeclaration {
  GlobalVariableDeclaration semicolon |
  GlobalConstantDeclaration semicolon |
  TypeAliasDeclaration semicolon |
  StructDeclaration |
  FunctionDeclaration
}

EnableExtension { TypeDeclaration | Reserved }

EnableDirective {
  d<'enable'> EnableExtension (comma EnableExtension)* comma? semicolon
}

ImportDeclaration {
  k<'import'> (ImportDeclarationList 'from')? String |
  k<'use'> String ('::' ImportDeclarationList)?
}
ImportDeclarationList { '{' (ImportDeclarationIdentifier comma)* ImportDeclarationIdentifier comma? '}' }
ImportDeclarationIdentifier { Identifier ('as' Identifier)? }

FunctionCall<content> { content }
Value<content> { content }

GlobalVariableDeclaration {
  AttributeList GlobalVariable (Assign Value<constExpression>)?
}
GlobalVariable { k<'var'> VariableQualifier? VariableIdentifier }

GlobalConstantDeclaration {
  AttributeList k<'const'> VariableIdentifier Assign Value<constExpression> |
  AttributeList k<'override'> VariableIdentifier (Assign Value<expression>)?
}

TypeAliasDeclaration { AttributeList k<'type'> Identifier (Assign TypeDeclaration)? }

StructDeclaration { AttributeList k<'struct'> Identifier StructBodyDeclaration? }
StructBodyDeclaration { '{' ((StructMember comma)* StructMember comma?)? '}' }
StructMember { AttributeList PrivateIdentifier colon TypeDeclaration }

FunctionDeclaration { AttributeList FunctionHeader CompoundStatement? }
FunctionHeader { k<'fn'> Identifier '(' ParamList ')' ReturnType }
ReturnType { (arrow AttributeList TypeDeclaration)? }

ParamList { ((Param comma)* Param comma?)? }
Param { AttributeList Identifier colon TypeDeclaration }

VariableIdentifier { Identifier | Identifier colon TypeDeclaration }
VariableQualifier { template<addressSpace (comma accessMode)?> }

variableStatement {
  k<'var'> VariableQualifier? VariableIdentifier (Assign Value<expression>)? |
  k<'let'> VariableIdentifier Assign Value<expression>
}

CompoundStatement { '{' Statement* '}' }

Statement {
  semicolon |
  returnStatement semicolon |
  ifStatement |
  switchStatement |
  loopStatement |
  forStatement |
  funcCallStatement semicolon |
  variableStatement semicolon |
  keywordStatement semicolon |
  assignmentStatement semicolon |
  CompoundStatement |
  incrementStatement semicolon |
  decrementStatement semicolon
}

assignmentStatement {
  lhsExpression (Assign | compoundAssignmentOperator) expression
}

compoundAssignmentOperator {
  AddAssign |
  SubAssign |
  MulAssign |
  DivAssign |
  ModAssign |
  AndAssign |
  XorAssign |
  OrAssign |
  LeftAssign |
  RightAssign 
}

incrementStatement { lhsExpression Inc }
decrementStatement { lhsExpression Dec }

ifStatement { k<'if'> expression CompoundStatement (k<'else'> elseStatement)? }
elseStatement { CompoundStatement | ifStatement }

switchStatement { k<'switch'> expression '{' switchBody+ '}' }
switchBody {
  k<'case'> caseSelectors colon '{' caseBody? '}' |
  k<'default'> colon '{' caseBody? '}'
}

caseSelectors { (constLiteral comma)* constLiteral comma? }

caseBody {
  Statement caseBody? |
  k<'fallthrough'> semicolon
}

loopStatement { k<'loop'> '{' Statement* ContinuingStatement? '}' }
forStatement { k<'for'> '(' forHeader ')' CompoundStatement }
forHeader { forInit? semicolon expression? semicolon forUpdate? }
forInit { variableStatement | assignmentStatement | funcCallStatement }
forUpdate { incrementStatement | decrementStatement | assignmentStatement | funcCallStatement }

ContinuingStatement { k<'continuing'> CompoundStatement }
returnStatement { k<'return'> expression? }
keywordStatement { (k<'break'> | k<'continue'> | k<'discard'>) }

funcCallStatement { Identifier argumentExpressionList }



expression {
  relationalExpression |
  shortCircuitOrExpression OrOr relationalExpression |
  shortCircuitAndExpression AndAnd relationalExpression |
  binaryAndExpression And unaryExpression |
  binaryOrExpression Or unaryExpression |
  binaryXorExpression Xor unaryExpression
}

constExpression {
  TypeDeclaration '(' ((constExpression comma)* constExpression comma?)? ')' |
  constLiteral
}

primaryExpression {
  Reserved |
  Identifier |
  FunctionCall<Identifier> ~conflict argumentExpressionList |
  TypeDeclaration argumentExpressionList |
  constLiteral |
  parenExpression |
  k<'bitcast'> template<TypeDeclaration> parenExpression
}

parenExpression { '(' expression ')' }
argumentExpressionList { '(' ((expression comma)* expression comma?)? ')' }

postfixExpression {
  LeftBracket expression RightBracket postfixExpression? |
  FieldAccess postfixExpression?
}

FieldAccess { '.' PrivateIdentifier }
PrivateIdentifier { Identifier }

unaryExpression {
  singularExpression |
  Sub unaryExpression |
  Bang unaryExpression |
  Tilde unaryExpression |
  Mul unaryExpression |
  And unaryExpression
}

singularExpression { primaryExpression postfixExpression? }

lhsExpression { (Mul | And)* coreLhsExpression postfixExpression? }

coreLhsExpression {
  Identifier |
  '(' lhsExpression ')'
}

multiplicativeExpression {
  unaryExpression |
  multiplicativeExpression Mul unaryExpression |
  multiplicativeExpression Div unaryExpression |
  multiplicativeExpression Mod unaryExpression
}

additiveExpression {
  multiplicativeExpression |
  additiveExpression Add multiplicativeExpression |
  additiveExpression Sub multiplicativeExpression
}

shiftExpression {
  additiveExpression |
  unaryExpression Left unaryExpression |
  unaryExpression Right unaryExpression
}

relationalExpression {
  shiftExpression |
  shiftExpression Lt shiftExpression |
  shiftExpression Gt shiftExpression |
  shiftExpression Lte shiftExpression |
  shiftExpression Gte shiftExpression |
  shiftExpression Eq shiftExpression |
  shiftExpression Neq shiftExpression
}
 
shortCircuitAndExpression {
  relationalExpression |
  shortCircuitAndExpression AndAnd relationalExpression
}

shortCircuitOrExpression {
  relationalExpression |
  shortCircuitOrExpression OrOr relationalExpression
}

binaryOrExpression {
  unaryExpression |
  binaryOrExpression Or unaryExpression
}

binaryAndExpression {
  unaryExpression |
  binaryAndExpression And unaryExpression
}

binaryXorExpression {
  unaryExpression |
  binaryXorExpression Xor unaryExpression
}

AttributeList { Attribute* }

Attribute {
  at Identifier (
    '(' (literalOrIdentifier comma)* literalOrIdentifier ')'
  )?
}

literalOrIdentifier {
  IntLiteral |
  UintLiteral |
  FloatLiteral |
  Identifier
}

constLiteral {
  IntLiteral |
  UintLiteral |
  FloatLiteral |
  b<'true'> |
  b<'false'>
}

elementCountExpression {
  IntLiteral |
  UintLiteral |
  Identifier
}

addressSpace {
  k<'function'> |
  k<'private'> |
  k<'workgroup'> |
  k<'uniform'> |
  k<'storage'>
}

accessMode {
  k<'read'> |
  k<'write'> |
  k<'read_write'>
}

TypeDeclaration {
  Identifier ~conflict |
  t<'array'> |
  t<'bool'> |
  t<'f16'> |
  t<'f32'> |
  t<'i32'> |
  t<'u32'> |
  t<'vec2'> template<TypeDeclaration>? |
  t<'vec3'> template<TypeDeclaration>? |
  t<'vec4'> template<TypeDeclaration>? |
  t<'vec2i'> |
  t<'vec3i'> |
  t<'vec4i'> |
  t<'vec2u'> |
  t<'vec3u'> |
  t<'vec4u'> |
  t<'vec2f'> |
  t<'vec3f'> |
  t<'vec4f'> |
  t<'vec2h'> |
  t<'vec3h'> |
  t<'vec4h'> |
  t<'ptr'> template<addressSpace comma TypeDeclaration (comma accessMode)?> |
  arrayTypeDeclaration |
  t<'mat2x2'> template<TypeDeclaration>? |
  t<'mat2x3'> template<TypeDeclaration>? |
  t<'mat2x4'> template<TypeDeclaration>? |
  t<'mat3x2'> template<TypeDeclaration>? |
  t<'mat3x3'> template<TypeDeclaration>? |
  t<'mat3x4'> template<TypeDeclaration>? |
  t<'mat4x2'> template<TypeDeclaration>? |
  t<'mat4x3'> template<TypeDeclaration>? |
  t<'mat4x4'> template<TypeDeclaration>? |
  t<'atomic'> template<TypeDeclaration> |
  textureSamplerTypes
}

arrayTypeDeclaration { t<'array'> '<' TypeDeclaration (comma elementCountExpression)? '>' }

textureSamplerTypes {
  samplerType |
  depthTextureType |
  sampledTextureType template<TypeDeclaration> |
  multisampledTextureType template<TypeDeclaration> |
  storageTextureType template<texelFormat comma accessMode>
}

samplerType {
  t<'sampler'> |
  t<'sampler_comparison'>
}

sampledTextureType {
  t<'texture_1d'> |
  t<'texture_2d'> |
  t<'texture_2d_array'> |
  t<'texture_3d'> |
  t<'texture_cube'> |
  t<'texture_cube_array'>
}

multisampledTextureType { k<'texture_multisampled_2d'> }
storageTextureType {
  t<'texture_storage_1d'> |
  t<'texture_storage_2d'> |
  t<'texture_storage_2d_array'> |
  t<'texture_storage_3d'>
}

depthTextureType {
  k<'texture_depth_2d'> |
  k<'texture_depth_2d_array'> |
  k<'texture_depth_cube'> |
  k<'texture_depth_cube_array'> |
  k<'texture_depth_multisampled_2d'>
}

texelFormat {
  t<'rgba8unorm'> |
  t<'rgba8snorm'> |
  t<'rgba8uint'> |
  t<'rgba8sint'> |
  t<'rgba16uint'> |
  t<'rgba16sint'> |
  t<'rgba16float'> |
  t<'r32uint'> |
  t<'r32sint'> |
  t<'r32float'> |
  t<'rg32uint'> |
  t<'rg32sint'> |
  t<'rg32float'> |
  t<'rgba32uint'> |
  t<'rgba32sint'> |
  t<'rgba32float'>
}

@tokens {
  space { std.whitespace+ }
  commentLine { "//" }
  commentOpen { "/*" }

  Identifier { $[a-zA-Z_] $[0-9a-zA-Z_]* }
  String { '"' ( !["]+ | '\\"' )* '"' | "'" ( ![']+ | "\\'" )* "'" }

  IntLiteral { "-"? "0" $[xX] $[0-9a-fA-F]+ | "0" | $[1-9] $[0-9]* }
  UintLiteral { "-"? "0" $[xX] $[0-9a-fA-F]+ "u" | "0u" | $[1-9] $[0-9]* "u" }
  FloatLiteral { 
    (
      "-"? 
      ($[0-9]* "." $[0-9]+ | "-"? $[0-9]+ "." $[0-9]*)
      ($[eE] $[+\-]? $[0-9]+)?
      "f"?
    ) |
    (
      "-"? 
      $[0-9]+
      $[eE] $[+\-]? $[0-9]+
      "f"?
    ) |
    (
      "-"? 
      "0" $[xX]
      (
        (
          (
            $[0-9a-fA-F]* "." $[0-9a-fA-F]+ |
            $[0-9a-fA-F]+ "." $[0-9a-fA-F]*
          )
          (
            $[pP] $[+\-]? $[0-9]+ "f"?
          )?
        ) |
        (
          $[0-9a-fA-F]+ $[pP] $[+\-]? $[0-9]+ "f"?
        )
      )
    )
  }
 
  comma { "," }
  colon { ":" }
  semicolon { ";" }
  at { "@" }
  arrow { "->" }

  Assign { "=" }
  AddAssign { "+=" }
  SubAssign { "-=" }
  MulAssign { "*=" }
  DivAssign { "/=" }
  ModAssign { "%=" }
  LeftAssign { "<<=" }
  RightAssign { ">>=" }
  AndAssign { "&=" }
  XorAssign { "^=" }
  OrAssign { "|=" }

  Add { "+" }
  Sub { "-" }
  Mul { "*" }
  Div { "/" }
  Mod { "%" }
  Left { "<<" }
  Right { ">>" }
  And { "&" }
  Xor { "^" }
  Or { "|" }

  AndAnd { "&&" }
  OrOr { "||" }
  
  Inc { "++" }
  Dec { "--" }
  Bang { "!" }
  Tilde { "~" }

  Eq { "==" }
  Neq { "!=" }
  Lt { "<" }
  Lte { "<=" }
  Gt { ">" }
  Gte { ">=" }

  LeftBracket { "[" }
  RightBracket { "]" }

  @precedence {
    commentLine, Div,
    FloatLiteral, UintLiteral, IntLiteral, Sub,
    Identifier
  }
}

Reserved {
  r<'asm'> |
  r<'bf16'> |
  r<'do'> |
  r<'enum'> |
  r<'f64'> |
  r<'handle'> |
  r<'i8'> |
  r<'i16'> |
  r<'i64'> |
  r<'mat'> |
  r<'premerge'> |
  r<'regardless'> |
  r<'typedef'> |
  r<'u8'> |
  r<'u16'> |
  r<'u64'> |
  r<'unless'> |
  r<'using'> |
  r<'vec'> |
  r<'void'> |
  r<'while'>
}
